--! styles names.styles.json

query: namesQuery is table('bigquery-public-data.usa_names.usa_1910_2013') -> {
  group_by: [
    decade is floor(`year`/10)*10,
    state,
    baby_gender is gender: pick 'Male' when ='M' else 'Female',
    gender
  ]
  aggregate: cohort_population is `number`.sum()
  nest: by_name is { group_by: name; aggregate: population is `number`.sum() }
} 

explore: names is from(->namesQuery) {
  dimension: name is concat(by_name.name,'') // !?BUG !?

  measure: [
    births_per_100k is FLOOR(by_name.population.sum()/cohort_population.sum() *100000),
    total_cohort_population is cohort_population.sum(),
    total_population is by_name.population.sum()
  ]
  dimension: name is concat(by_name.name,'') -- bug

  query: state_map is { group_by: state; aggregate: births_per_100k }
  query: decade_graph is {
    group_by: decade
    aggregate: births_per_100k
    group_by: baby_gender
  }
  -- -- explore names | name_list
  query: name_dashboard is {
    top: 15
    group_by: name
    aggregate: total_population
    nest: [ state_map, decade_graph ]
  }

  query: location_iconic_names is {
    group_by: [ name, baby_gender ]
    aggregate: births_per_100k
    nest: by_state is { group_by: state; aggregate: births_per_100k }
  } -> {
    where: births_per_100k > 50
    group_by: by_state.state
    nest: by_gender is {
      group_by: baby_gender
      nest: iconic_name_list is {
        top: 15; order_by: 2 desc
        group_by: name
        aggregate: popularity is (by_state.births_per_100k - births_per_100k) /births_per_100k
      }
    }
  }

  query: time_iconic_names is {
    group_by: [name, baby_gender]
    aggregate: births_per_100k
    nest: by_decade is {
      group_by: decade
      aggregate: births_per_100k
    } 
  } 
  -> {
    where: births_per_100k > 50
    group_by: by_decade.decade
    nest: by_gender is {
      group_by: baby_gender
      nest: iconic_name_list is {
        top: 50; order_by: 2 desc
        group_by: name
        aggregate: popularity is (by_decade.births_per_100k - births_per_100k) /births_per_100k
      }
    }  
  }
}

query:  jes_dashboard is names -> name_dashboard {? name ~ r'Jes' }

query: jessie_over_time is names -> {
  where: name = 'Jessie'
  nest: decade_graph
  nest: total_population
  nest: by_state_and_time is {
    group_by: state
    aggregate: total_population
    nest: decade_graph
  }
}

query: alabama_iconic_names_by_decade is names -> time_iconic_names {? state: 'AL' }

query: newyork_iconic_names_by_decade is names -> time_iconic_names {? state: 'NY' }

query: old_iconic_names_by_state is names -> location_iconic_names {? decade <= 1940 }