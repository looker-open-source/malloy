--! styles airports.styles.json

explore: airports is table('malloy-data.faa.airports') {
  measure: airport_count is count()

  query: by_state is {
    where: state != null
    group_by: state
    aggregate: airport_count
  }

  query: by_fac_type is {
    group_by: fac_type
    aggregate: [
      airport_count
      avg_elevation is elevation.avg()
    ]
  }

  query: by_state_and_county is {
    group_by: state
    aggregate: airport_count
    nest: by_fac_type
    nest: by_county is { 
      top: 5
      group_by: county
      aggregate: airport_count
    }
  }

  query: major_airports is {
    where: major = 'Y'
    group_by: name is concat(state, '-', full_name)
  }

  query: airports_by_region is {
    where: faa_region != null
    group_by: faa_region
    aggregate: airport_count
    nest: [
      by_fac_type
      by_state
      major_airports
    ]
  }
}
