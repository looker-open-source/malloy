--! styles hackernews.styles.json

import "hackernews.malloy"

-- analysis about FAANG (Facebook Apple Amazon Netflix and Google)
-- a resonable threshold for interesting stories is about 10

explore: faang_analysis is stories {

  dimension: [
    faang is title :
      pick 'Facebook' when ~ r'(Facebook|Instagram|Whatsapp)'
      pick 'Apple' when ~ r'(Apple|iPhone|IOS|Macbook)'
      pick 'Amazon' when ~ r'(Amazon|AWS)'
      pick 'Netflix' when ~ r'Netflix'
      pick 'Google' when ~ r'(Google|GOOG|Alphabet|GCloud|Chrome)'
      else 'OTHER'
  ]

  query: apple_dashboard is term_dashboard {
    where: [faang = 'Apple', score >= threshold]
  }

  query: google_dashboard is term_dashboard {
    where: [faang = 'Google', score >= threshold]
  }

  query: faang_stories is stories {
    where: [faang != 'OTHER', score >= threshold]
  }

  query: faang_dashboard is {
    where: [faang != 'OTHER',
      score >= threshold,
      post_type = 'story'
    ]
    nest: faang_story_chart is {
      group_by: posted_month is post_time.month
      aggregate: post_count
      group_by: faang
    }
    nest: facebook is top_sites {where: faang = 'Facebook'}
    nest: apple is top_sites {where: faang = 'Apple'}
    nest: amazon is top_sites {where: faang = 'Amazon'}
    nest: netflix is top_sites {where: faang = 'Netflix'}
    nest: google is top_sites {where: faang = 'Google'}
  }

  query: poster_dashboard2 is poster_dashboard {where: faang != 'OTHER'}

}